dnl
dnl  Makefile for the GNOKII tool suite.
dnl 
dnl  Copyright (C) 1999 Hugh Blemings & Pavel Janík ml.
dnl                2000 Karel Zak, Pawe³ Kot
dnl                2002 BORBELY Zoltan
dnl
dnl  $Id$
dnl

AC_INIT(gnokii/gnokii.c)

AC_CONFIG_AUX_DIR(config)

AC_CANONICAL_SYSTEM
AC_PREFIX_DEFAULT("/usr/local")

dnl ======================== Checks for programs.
AC_PROG_CC
AC_PROG_CPP
AC_PROG_LEX
AC_PATH_PROG(RM, rm, no)
AC_PATH_PROG(FIND, find, no)
AC_CHECK_PROGS(MAKE, gmake make)
AC_PROG_INSTALL

dnl ======================== Default setting
ALL_LINGUAS="cs de et fi nl sk pl it sl"
AM_GNU_GETTEXT

dnl ======================== Check for include/config.h.in
AC_MSG_CHECKING([for include/config.h.in])
if test -f include/config.h.in; then
	AC_MSG_RESULT(yes)
else
	AC_MSG_RESULT(no)
	echo '*************************************************'
	echo 'include/config.h.in missing, run autoheader first'
	echo 'autoheader is part of the autoconf package'
	echo '*************************************************'
	exit 1
fi

dnl Let us have $prefix variable aviable here
test x"$prefix" = xNONE && prefix="$ac_default_prefix"

AC_ARG_ENABLE(debug, 
	[  --enable-debug          compile with debug code],
	[ if test "x$GCC" = "xyes"; then
		CFLAGS="-ggdb3 -Wall"
	  fi
	  AC_DEFINE(DEBUG)
	  debug="yes" ],
	[ debug="no"  ]
)

AC_ARG_ENABLE(xdebug,
	[  --enable-xdebug         compile with xdebug code],
	[ AC_DEFINE(XDEBUG)
	  xdebug="yes" ],
	[ xdebug="no"  ]
)

AC_ARG_ENABLE(rlpdebug, 
	[  --enable-rlpdebug       compile with RLP debug code],
	[ AC_DEFINE(RLP_DEBUG)
	  rlpdebug="yes" ],
	[ rlpdebug="no"  ]
)

dnl ======================== Set CFLAGS if not set
if test "x$CFLAGS" = "x"; then
	CFLAGS="-O2 -Wall"
fi

dnl ======================== Checks for libraries.

AC_ARG_WITH(libpthread, 
   [  --with-libpthread=DIR   specifies the base libpthread],
   [ if test x$withval = xyes
     then 
      AC_MSG_WARN(Usage is: --with-libpthread=DIR)
     else
      PTHREAD_LIBS="-L$withval/lib/"
      PTHREAD_CFLAGS="-I$withval/include/"
     fi
   ]
)

dnl Check for libpthread
PTHREAD_LIBS_SAVE="$PTHREAD_LIBS"
PTHREAD_LIBS=error
dnl we cannot use AC_CHECK_LIB, HP-UX requires the #include statement -- bozo
AC_CACHE_CHECK(for pthread_attr_init in -lpthread, ac_cv_lib_pthread_pthread_attr_init,
	[AC_TRY_LINK([#include <pthread.h>],
		[pthread_attr_init(NULL);],
		ac_cv_lib_pthread_pthread_attr_init=yes,
		ac_cv_lib_pthread_pthread_attr_init=no)])
if test $ac_cv_lib_pthread_pthread_attr_init = yes; then
	PTHREAD_CFLAGS="$PTHREAD_CFLAGS -D_REENTRANT"
	PTHREAD_LIBS="$PTHREAD_LIBS_SAVE -lpthread"
fi

dnl FIXME: test this on *BSD and report results immediatelly to the ml please
dnl If it is not found, try to check for c_r (on FreeBSD)
if test "x$PTHREAD_LIBS" = xerror; then
  AC_CHECK_LIB(c_r, pthread_attr_init, [
                    PTHREAD_CFLAGS="$PTHREAD_CFLAGS -D_THREAD_SAFE"
                    PTHREAD_LIBS="-pthread" ])
fi

dnl FIXME: do we really test here if libc contains this function?
if test "x$PTHREAD_LIBS" = xerror; then
   AC_CHECK_FUNC(pthread_attr_init, PTHREAD_LIBS="",
      AC_MSG_ERROR(not found library: pthread !!!))
fi

AC_CACHE_CHECK(for tm_gmtoff in struct tm, ac_cv_have_tm_gmtoff,
	[AC_TRY_COMPILE([#include <time.h>],
		[struct tm t; t.tm_gmtoff = 0],
		ac_cv_have_tm_gmtoff=yes,
		ac_cv_have_tm_gmtoff=no)])
if test $ac_cv_have_tm_gmtoff = yes; then
	AC_DEFINE(HAVE_TM_GMTON)
fi

AC_CACHE_CHECK(for timer operations, ac_cv_have_timeops,
	[AC_TRY_LINK([#include <sys/time.h>],
		[struct timeval tv; timerisset(&tv); timerclear(&tv); timercmp(&tv, &tv, <); timeradd(&tv, &tv, &tv); timersub(&tv, &tv, &tv); ],
		ac_cv_have_timeops=yes,
		ac_cv_have_timeops=no)])
if test $ac_cv_have_timeops = yes; then
        AC_DEFINE(HAVE_TIMEOPS)
fi

have_termios="no"
dnl Checking for setspeed in termios.h
AC_CACHE_CHECK(for cfsetspeed in termios.h, ac_cv_have_cfsetspeed,
	[AC_TRY_LINK([#include <termios.h>],
		[struct termios t; cfsetspeed(&t, B9600);],
		ac_cv_have_cfsetspeed=yes,
		ac_cv_have_cfsetspeed=no)])
if test $ac_cv_have_cfsetspeed = yes; then
        AC_DEFINE(HAVE_CFSETSPEED)
        have_termios="yes"
fi

if test $have_termios = "no"; then
	AC_CACHE_CHECK(for cfsetispeed and cfsetospeed in termios.h, ac_cv_have_cfsetiospeed,
		[AC_TRY_LINK([#include <termios.h>],
			[struct termios t; cfsetispeed(&t, B9600);  cfsetospeed(&t, B9600);],
			ac_cv_have_cfsetiospeed=yes,
			ac_cv_have_cfsetiospeed=no)])
	if test $ac_cv_have_cfsetiospeed = yes; then
		AC_DEFINE(HAVE_CFSETISPEED)
		AC_DEFINE(HAVE_CFSETOSPEED)
		have_termios="yes"
	fi
fi

if test $have_termios = "no"; then
	AC_CACHE_CHECK(for c_ispeed and c_ospeed in struct termios, ac_cv_have_c_iospeed,
		[AC_TRY_COMPILE([#include <termios.h>],
			[struct termios t; t.c_iflag = B9600; t.c_oflag = B9600;],
			ac_cv_have_c_iospeed=yes,
			ac_cv_have_c_iospeed=yes)])
	if test ac_cv_have_c_iospeed = yes; then
		AC_DEFINE(HAVE_TERMIOS_CSPEED)
	fi
fi

dnl ======================== Checks for getopt_long support 

AC_CHECK_HEADER(getopt.h, ,
      [CFLAGS="$CFLAGS -I../getopt"])

AC_ARG_WITH(gnugetopt,
   [  --with-gnugetopt=DIR       specifies the getopt library location directory],
   [ if test x$withval=yes; then
        AC_MSG_WARN(Usage is: --with-gnugetopt=DIR) 
     else
        GETOPT_LIBS="-L$withval"
     fi
   ]
)

OWN_GETOPT=""
AC_CHECK_FUNC(getopt_long, , [
        LIBS="$LIBS $GETOPT_LIBS"
           AC_CHECK_LIB(gnugetopt, getopt_long, LIBS="$LIBS -lgnugetopt",
            OWN_GETOPT="1")])


dnl ======================== Checks for Linux IrDA support
AC_CHECK_HEADER(linux/irda.h,
	[AC_DEFINE(HAVE_IRDA)
	 USE_IRDA="yes"])

dnl ======================== Checks for Linux Bluetooth support
bluetooth="no"
AC_ARG_WITH(bluetooth,
	[  --with-bluetooth=DIR   specifies the base libbluetooth],
	[ if test x$withval = xyes; then
		AC_MSG_WARN(Usage is: --with-bluetooth=DIR)
	  else
		LDLIBS="-L$withval/lib/"
		CFLAGS="-I$withval/include/"
	  fi
	]
)

AC_MSG_CHECKING([for the bluetooth support])
echo
AC_CACHE_CHECK(for the struct sockaddr_rc in <bluetooth/rfcomm.h>, ac_cv_have_sockaddr_rc,
	[AC_TRY_COMPILE([#include <sys/socket.h>
			#include <bluetooth/bluetooth.h>
			#include <bluetooth/rfcomm.h>],
		[struct sockaddr_rc rc;],
		ac_cv_have_sockaddr_rc=yes,
		ac_cv_have_sockaddr_rc=no)])
if test $ac_cv_have_sockaddr_rc = yes; then
	AC_DEFINE(HAVE_BLUETOOTH)
	USE_BLUETOOTH="yes"
fi

dnl ======================== Checks for X base support 

if test "$no_x" = yes -o "$with_x" = "no"; then
   x_support="no"
   XPM_CFLAGS=""
   XPM_LIBS=""
   GTK_CFLAGS=""
   GTK_LIBS=""
else
   AC_PATH_X
   if test "x$x_includes" != "x"; then
      CPPFLAGS="$CPPFLAGS -I$x_includes"
   fi
   AC_CHECK_HEADERS(X11/xpm.h)

   if test "x$x_libraries" = "xNONE"; then
      x_support="no"
      XPM_CFLAGS=""
      XPM_LIBS=""
      GTK_CFLAGS=""
      GTK_LIBS=""
      AC_MSG_WARN(Cannot find library libX11.)
      AC_MSG_WARN(Disabling xgnokii.)
   else
      AC_CHECK_LIB(Xpm, XpmWriteFileFromXpmImage,
            [ XPM_CFLAGS="-I$x_includes" XPM_LIBS="-L$x_libraries -lXpm -lX11"
              AC_DEFINE(XPM) ],
            AC_MSG_WARN(Cannot found library libXpm - disabling XPM support.),
            [ -L$x_libraries -lX11 ]
      )

      AC_PATH_PROGS(GTK_CONFIG, gtk-config gtk12-config, no)
      if test "$GTK_CONFIG" = no; then
         x_support="no"
         GTK_CFLAGS=""
         GTK_LIBS=""
         AC_MSG_WARN(Cannot find gtk-config.)
         AC_MSG_WARN(Disabling xgnokii.)
      else
         GTK_CFLAGS=`$GTK_CONFIG --cflags`
         GTK_LIBS=`$GTK_CONFIG --libs`
         x_support="yes"   

         XGNOKIIDIR='${prefix}'
         AC_ARG_WITH(xgnokiidir,
            [  --with-xgnokiidir=DIR   specifies the base for xgnokii],
            [ if test x$withval = xyes; then 
                 AC_MSG_WARN(Usage is: --with-xgnokiilib=DIR)
              else
                 XGNOKIIDIR="$withval"
              fi
            ]
         )
      fi
   fi
fi

dnl ======================== Check for libsocket
AC_CHECK_LIB(socket, socket)

dnl ======================== Additional switches

AC_ARG_ENABLE(security, 
   [  --enable-security       enable all security features ],
   [ AC_DEFINE(SECURITY)
     security="yes" ],
   [ security="no"  ]
)

dnl TODO: We should be checking for GNU ld and only using GNU specific
dnl paramaters if it's found.
AC_ARG_ENABLE(shared,
   [  --disable-shared        build without shared library ],
   [ NO_SHARED=1
     shared="no"
     GLDFLAGS="" ],
   [ shared="yes"
     GLDFLAGS="-Wl,--rpath -Wl,\$(libdir)"]
)

AC_ARG_ENABLE(win32, 
   [  --enable-win32          if you want win32 suport ],
   [ AC_DEFINE(WIN32)
     WIN32=1
     win32="-DWIN32" ],
   [ win32=""]
)

AC_CHECK_FUNC(grantpt,
   [ AC_TRY_RUN([
#define  _XOPEN_SOURCE 500

#include <stdlib.h>
#include <sys/types.h>
#include <sys/stat.h>
#include <fcntl.h>
#include <unistd.h>

int main()
{
	char *name = NULL;
        int master, err;

        master = open("/dev/ptmx", O_RDWR | O_NOCTTY | O_NONBLOCK);
        if (master >= 0) {
                err = grantpt(master);
                err = err || unlockpt(master);
                if (!err) {
                        name = ptsname(master);
                } else {
                        exit(-1);
                }
        } else {
		exit(-1);
	}
	close(master);
	exit(0);
}
                ],
                AC_DEFINE(USE_UNIX98PTYS),
                AC_MSG_WARN("No unix98ptys"),
		AC_MSG_WARN("Ensure to disable unix98ptys when crosscompiling"))
   ]
)

AC_TRY_COMPILE( [#define _XOPEN_SOURCE 500
		 #include <sys/types.h>
		 #include <sys/socket.h>],
		[struct msghdr msg; msg.msg_control;],
		AC_DEFINE(HAVE_MSGHDR_MSG_CONTROL)
)

dnl ======================== Checks for header files.
AC_HEADER_STDC
AC_HEADER_SYS_WAIT
AC_CHECK_HEADERS(fcntl.h sys/ioctl.h sys/time.h unistd.h)
AC_CHECK_HEADERS(string.h strings.h ctype.h stdlib.h stdarg.h)
AC_CHECK_HEADERS(stddef.h sys/socket.h sys/modem.h termios.h sys/filio.h)

dnl ======================== Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_C_INLINE
AC_TYPE_PID_T
AC_TYPE_SIZE_T
AC_HEADER_TIME
AC_STRUCT_TM
AC_CACHE_CHECK(for long long, ac_cv_c_long_long,
	[AC_TRY_RUN([int main() { long long foo = 0; exit(sizeof(long long) < sizeof(long)); }],
		ac_cv_c_long_long=yes, ac_cv_c_long_long=no, ac_cv_c_long_long=no)])
if test $ac_cv_c_long_long = yes; then
	AC_DEFINE(HAVE_LONG_LONG)
fi
AC_CACHE_CHECK(for long double, ac_cv_c_long_double,
	[AC_TRY_RUN([int main() { long double foo = 0.0; exit(sizeof(long double) < sizeof(double)); }],
		ac_cv_c_long_double=yes, ac_cv_c_long_double=no, ac_cv_c_long_double=no)])
if test $ac_cv_c_long_double = yes; then
	AC_DEFINE(HAVE_LONG_DOUBLE, 1, [Define if your compiler supports the \`long double' type.])
fi

AC_CACHE_CHECK(for __ptr_t, ac_cv_c___ptr_t,
	[AC_TRY_COMPILE([#include <stdio.h>],
		[__ptr_t foo = NULL;],
		ac_cv_c___ptr_t=yes, ac_cv_c___ptr_t=no)])
if test $ac_cv_c___ptr_t = yes; then
	AC_DEFINE(HAVE_PTR_T, 1)
fi

dnl ======================== Checks for library functions.
AC_PROG_GCC_TRADITIONAL
AC_FUNC_MEMCMP
AC_TYPE_SIGNAL
AC_FUNC_STRFTIME
AC_CHECK_FUNCS(mktime gettimeofday select poll)
AC_CHECK_FUNCS(strdup strstr strtol strtok strsep asprintf vasprintf)
AC_CHECK_FUNCS(snprintf vsnprintf)
AC_CACHE_CHECK(for ISO C99 compliant snprintf,ac_cv_func_snprintf_c99,
	[AC_TRY_RUN([
#include <stdio.h>

int main()
{
	char buf[] = {0, 0, 0, 0};

	snprintf(buf, 3, "ABC");
	exit ((buf[2] != 0) || (snprintf(NULL, 0, "%d", 100) != 3));
}],ac_cv_func_snprintf_c99=yes,ac_cv_func_snprintf_c99=no,ac_cv_func_snprintf_c99=no)])
if test $ac_cv_func_snprintf_c99 = yes; then
	AC_DEFINE(HAVE_C99_SNPRINTF)
fi
AC_CACHE_CHECK(for ISO C99 compliant vsnprintf,ac_cv_func_vsnprintf_c99,
	[AC_TRY_RUN([
#include <stdio.h>
#include <stdarg.h>

int doit(char *buf, int len, const char *s, ...)
{
	va_list ap;
	int r;

	va_start(ap, s);
	r = vsnprintf(buf, len, s, ap);
	va_end(ap);

	return r;
}

int main()
{
	char buf[] = {0, 0, 0, 0};

	doit(buf, 3, "ABC");
	exit ((buf[2] != 0) || (doit(NULL, 0, "%d", 100) != 3));
}],ac_cv_func_vsnprintf_c99=yes,ac_cv_func_vsnprintf_c99=no,ac_cv_func_vsnprintf_c99=no)])
if test $ac_cv_func_vsnprintf_c99 = yes; then
	AC_DEFINE(HAVE_C99_VSNPRINTF)
fi

CFLAGS="$CFLAGS $NLS_CFLAGS"
LIBS="$LIBS $NLS_LIBS"

PACKAGE=gnokii
XPACKAGE=xgnokii
VERSION=`cat VERSION`
XVERSION=`cat xgnokii/VERSION`
AC_DEFINE_UNQUOTED(VERSION, "$VERSION")
AC_DEFINE_UNQUOTED(XVERSION, "$XVERSION")
if test "$x_support" = yes; then
	AC_DEFINE_UNQUOTED(XGNOKIIDIR, "`eval echo $XGNOKIIDIR`")
fi
HAVE_XGNOKII=$x_support

case "$INSTALL" in
  'config/install-sh -c') INSTALL=`pwd`/$INSTALL
   ;;
esac

SHELL=${CONFIG_SHELL-/bin/sh}

AC_SUBST(SHELL)
case "$build_os" in
  solaris*) SHELL=/bin/ksh
   ;;
esac

AC_CONFIG_HEADER(include/config.h)

AC_SUBST(PACKAGE)
AC_SUBST(VERSION)
AC_SUBST(XVERSION)
AC_SUBST(XPACKAGE)

AC_SUBST(XGNOKIIDIR)
AC_SUBST(exec_prefix)

AC_SUBST(NO_SHARED)
AC_SUBST(GLDFLAGS)

AC_SUBST(PTHREAD_CFLAGS)
AC_SUBST(PTHREAD_LIBS)

AC_SUBST(OWN_GETOPT)
AC_SUBST(USE_UNIX98PTYS)

AC_SUBST(GTK_CFLAGS)
AC_SUBST(GTK_LIBS)
AC_SUBST(XPM_CFLAGS)
AC_SUBST(XPM_LIBS)
AC_SUBST(HAVE_XGNOKII)
AC_SUBST(WIN32)

AC_OUTPUT(
   Makefile.global
   po/Makefile.in
   po/Makefile
   packaging/RedHat/gnokii.spec
   packaging/Slackware/SlackBuild
   )


dnl ======================== Final report

echo "

  G N O K I I

  A Linux/Unix toolset and driver for Nokia mobile phones.

  Copyright (C) 1999-2002  The Gnokii Development Team.

  This program is free software; you can redistribute it and/or modify
  it under the terms of the GNU General Public License as published by
  the Free Software Foundation; either version 2 of the License, or
  (at your option) any later version.
 
  See file COPYING for more details.

    Host system:        $host_os
    Gnokii version:     $VERSION
    Xgnokii version:    $XVERSION
    X (GTK) support:    $x_support
    Debug:              $debug
    XDebug:             $xdebug
    RLPDebug:           $rlpdebug
    NLS:                $USE_NLS
    IrDA:               $USE_IRDA
    Bluetooth:          $USE_BLUETOOTH
    Security:           $security
    Build Shared Lib:   $shared
    Win32:              $win32 
    Prefix:             $prefix

  Type '${MAKE}' for compilation and then '${MAKE} install',
  '${MAKE} install-suid', '${MAKE} install-strip' or '${MAKE} install-ss' to
  install gnokii. If you wish to install gnokii sgid gnokii (install-suid or
  install-ss) be sure to have 'gnokii' group in your system.
"
